# pavSharedApt

if night_mode = 1:
	fcolor = rgb(255, 255, 255)
	bcolor = rgb(0, 0, 0)
	lcolor = rgb(106, 90, 205)
else
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
end

if $ARGS[0] = 'update':
	rentWeekly = 2000

	if rentLeft < 0:rentLeft = 0
		!how much of the unpaid weekly rent is left
	if orgB > 100:orgB = 100

	price_cook = 50
	price_clean = 50
	price_handjob = 50
	price_blowjob = 100
	price_vaginal = 150
	price_anal = 200
	price_special = 250

	! time calculations
	weekNum = (daystart - (daystart mod 7)) / 7

	! pcs setup
	$pcs_font = '990099'
		!pink

	! servitudeLvl = 0 - no service, pays rent
	! servitudeLvl = 1 - nonsexual service
	! servitudeLvl = 2 - sexual and nonsexual service

	! nerd guy
	! shy, polite, but latent kink (giving golden shower, etc.)
	! brown hair, hairy, pale skin
	! average dick, hairy balls
	! not paying attention to his own image and looks, wears glasses
	$nd_name = 'Oleg'
	$nd_font = '0096FF'
		!blue
	nd_leaves = 9
	nd_comes = 18
	nd_sleeps = 23
	nd_wakes = 8
	if nd_rel > 100: nd_rel = 100
	if nd_rel < 0: nd_rel = 0
	ndDick = 17
	nd_cumMax = 80
	nd_cumMin = 15
	if nd_cumVol > nd_cumMax:nd_cumVol = nd_cumMax
	if nd_cumVol < nd_cumMin:nd_cumVol = nd_cumMin
	nd_horny = (nd_cumVol * 10000 / nd_cumMax) / 100

	if nd_served = 1:nd_hadSex = 1
		!flag if there was already a sex between him and Sveta
		!sex flags: nd_bj ; nd_bjDeep ; nd_swall ; nd_vag ; nd_vagCumInside ; nd_vagCumInsideUnwanted ; nd_anal ; nd_analCumInside ; nd_analCumInsideUnwanted
		!unlocked sex: nd_doAnal ; nd_doPornpos
		!nd_cleaned = 1 - if was cleaned this day already

	! gopnik guy
	! rude, the younger the better, aggresive, hard
	! dark hair, dark skin
	! above average dick, somehow maintained hairs; event: he will take some enlarging pills testing his monster cock on Sveta
	$gp_name = 'Nestor'
	$gp_font = 'CC0000'
		!red
	gp_leaves = 11
	gp_comes = 19
	gp_sleeps = 23
	gp_wakes = 9
	if gp_rel > 100: gp_rel = 100
	if gp_rel < 0: gp_rel = 0
	gpDick = 22
	gp_cumMax = 100
	gp_cumMin = 30
	if gp_cumVol > gp_cumMax:gp_cumVol = gp_cumMax
	if gp_cumVol < gp_cumMin:gp_cumVol = gp_cumMin
	gp_horny = (gp_cumVol * 10000 / gp_cumMax) / 100
	if gp_served = 1:gp_hadSex = 1
		!flag if there was already a sex between him and Sveta
		!gp_bj ; gp_bjDeep ; gp_swall ; gp_vag ; gp_vagCumInside ; gp_vagCumInsideUnwanted ; gp_anal ; gp_analCumInside ; gp_analCumInsideUnwanted
		!unlocked sex: gp_doAnal ; gp_doPornpos ; gp_doVacuum

	! sportsman guy
	! blond hair, tanned
	! latent gay, muscular and athletic, secret fantasies of being used by femboy (or at least a girl with strapon)
	! bellow average dick, shaved
	$sp_name = 'Yakov'
	$sp_font = '009900'
		!green
	sp_leaves = 7
	sp_comes = 16
	sp_sleeps = 22
	sp_wakes = 5
	if sp_rel > 100: sp_rel = 100
	if sp_rel < 0: sp_rel = 0
	spDick = 14
	sp_cumMax = 60
	sp_cumMin = 10
	sp_horny = (sp_cumVol * 10000 / sp_cumMax) / 100
	if sp_cumVol > sp_cumMax:sp_cumVol = sp_cumMax
	if sp_cumVol < sp_cumMin:sp_cumVol = sp_cumMin
	if sp_served = 1:sp_hadSex = 1
		!flag if there was already a sex between him and Sveta
		!sp_bj ; sp_bjDeep ; sp_swall ; sp_vag ; sp_vagCumInside ; sp_vagCumInsideUnwanted ; sp_anal ; sp_analCumInside ; sp_analCumInsideUnwanted

	!mood setup
	if nd_mood = 1:$nd_mood = 'very good'
	if nd_mood = 2:$nd_mood = 'good'
	if nd_mood = 3:$nd_mood = 'normal'
	if nd_mood = 4:$nd_mood = 'bad'
	if nd_mood = 5:$nd_mood = 'very bad'

	if gp_mood = 1:$gp_mood = 'very good'
	if gp_mood = 2:$gp_mood = 'good'
	if gp_mood = 3:$gp_mood = 'normal'
	if gp_mood = 4:$gp_mood = 'bad'
	if gp_mood = 5:$gp_mood = 'very bad'

	if sp_mood = 1:$sp_mood = 'very good'
	if sp_mood = 2:$sp_mood = 'good'
	if sp_mood = 3:$sp_mood = 'normal'
	if sp_mood = 4:$sp_mood = 'bad'
	if sp_mood = 5:$sp_mood = 'very bad'

	gs 'stat'

	! if debg = 0:
		! '<a href="exec:debg=1">DEBUG</a>'
	! elseif debg = 1:
		! '<a href="exec:debg=0">DEBUG</a>'
		! $nd_name+' relation: '+nd_rel
		! !$gp_name+' relation: '+gp_rel
		! !$sp_name+' relation: '+sp_rel
		! 'rentLeft: '+rentLeft
		! 'weekNum: '+weekNum
		! 'rentWeekNum: '+rentWeekNum
		! 'servitudeLvl: '+servitudeLvl
		! 'servicePaid: '+servicePaid
		! 'cashPaid: '+cashPaid
		! 'step: '+step
		! 'subStep: '+subStep
		! 'gp_mood: '+gp_mood
		! 'gp_cumVol: '+gp_cumVol
		! 'gp_cumVag: '+gp_cumVag
		! 'gp_cumAnal: '+gp_cumAnal
		! 'gp_cumSwall: '+gp_cumSwall
		! 'nd_cumVol: '+nd_cumVol
		! 'nd_cumMax: '+nd_cumMax
		! 'nd_horny: '+nd_horny
		! 'gp_horny: '+gp_horny
		! 'sp_horny: '+sp_horny
		! ''
		! nd_served = 0
		! nd_mood = 2
		! gp_served = 0
		! gp_mood = 2
		! sp_mood = 2
		! sp_served = 0
	! end
end

! accomodation advert
if $ARGS[0] = 'advertNo30':
	minut += 2
	seenAd = 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/aptNo3Door.jpg"></center>'
	'<br>As you pass by one of the apartments, you notice a sign stuck on the door - "сдаeтся в аренду комната" (Room for rent!).'
	act 'Check it out':gt 'pavSharedApt', 'doorNo30'
	act 'Go back':gt 'pavComplexb3', 'etaj_4'
end

! door No 3 - shared
if $ARGS[0] = 'doorNo30':
	if pavSharedAptEnabled = 1:
		'<center><img <<$set_imgh>> src="images/locations/shared/apartment/podezdhr.jpg"></center>'
		'<br>You stand before the entry door to apartment No 30. The apartment you share with three others'
		act 'Go inside':gt 'pavSharedApt', 'start'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/aptNo3Door.jpg"></center>'
		'<br>You stand before the entry door to apartment No 30. There is a sign on the door saying - "сдаeтся в аренду комната" (Room for rent).'
		act 'Ring the bell':gt 'pavSharedApt', 'intro'
	end
	act 'Go back':gt 'pavComplexb3', 'etaj_4'
end

! apt hall
if $ARGS[0] = 'start':
	$metka = 'start'
	$goBack = $metka
	$metkaM = 'start'
	$locM = 'pavSharedApt'
	$loc = 'pavSharedApt'
	gs 'pavSharedApt', 'update'
	if introDone = 0:gt 'pavSharedApt', 'intro'
	minut += 2
	gs 'stat'

	! do this once a day
	if daystart >= tomorrow:
		tomorrow = daystart + 1

		nd_mood = rand (1, 5)
		nd_served = 0
		nd_cleaned = 0
		nd_cumVol += rand(15, 30)

		gp_mood = rand (1, 5)
		gp_served = 0
		gp_cleaned = 0
		gp_cumVol = rand(20, 40)

		sp_mood = rand (1, 5)
		sp_served = 0
		sp_cleaned = 0
		sp_cumVol += rand(5, 20)
	end

	! do this every time
	rentLeft = rentWeekly - servicePaid - cashPaid

	'<center><h3>Hallway</h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/hallway.jpg"></center>'
	'It is week number '+weekNum+'.'

	if weekNum < rentWeekNum:
		if rentLeft > 0:
			'You have left '+rentLeft+' <b>₽</b> of rent to pay for the next week.'
			if servitudeLvl >= 1:
				'You may still pay it in <a href="exec:gt ''pavSharedApt'', ''payCash''">cash</a>, if you want.'
			else
				'Pay it in <a href="exec:gt ''pavSharedApt'', ''payCash''">cash</a>.'
			end
		else
			'You have already paid the whole rent for the next week.'
		end

	elseif weekNum >= rentWeekNum:
		if rentLeft = 0:
			!rent was forepaid, everything OK, proceed to next week
			rentLeft += 2000
			cashPaid = 0
			rentWeekNum = weekNum + 1
		elseif rentLeft > 0:
			!rent was not fully paid
			'You did not pay the whole rent for this week.'
			if money >= rentLeft:
				!not enough cash to pay it manually
				'You still can pay it in <a href="exec:gt ''pavSharedApt'', ''payCash''">cash</a>, otwerwise you''ll be kicked out!'
			else
				!in future add some way to serve your way out
				'You do not have enough money to pay the rest of the rent in cash, even if you wanted to. Guess you''ll have to find a new living place..'
				rentPaid = 0
				pavSharedAptEnabled = 0
			end
		end
	end

	if rentPaid = 1:
		'A large apartment currently serves as shared living. As well as the normal living spaces, there are four bedrooms, one rented by you and three occupied by <a href="exec: gt ''pavShared_nd'', ''ndDick''"><font color=#<<$nd_font>>><<$nd_name>></font></a>, <a href="exec: gt ''pavShared_gp'', ''gpDick''"><font color=#<<$gp_font>>><<$gp_name>></font></a>, <a href="exec: gt ''pavShared_sp'', ''spDick''"><font color=#<<$sp_font>>><<$sp_name>></font></a>.'
		'You stand in the hallway. '
		''
		'From the hallway several doors lead to <a href="exec:gt ''pavSharedApt'', ''ndRoom''"><<$nd_name>>''s</a>, <a href="exec:gt ''pavSharedApt'', ''gpRoom''"><<$gp_name>>''s</a> and <a href="exec:gt ''pavSharedApt'', ''spRoom''"><<$sp_name>>''s</a> rooms.'
		'The last doors lead to the <a href="exec:gt ''pavSharedApt'', ''pcsRoom''">room</a> you have rented.'
		''
		'There are several <a href="exec:gt ''wardrobe'', ''start''">closets</a>, that you can use to <a href="exec:gt ''clothing'', ''view_clothing_list'', ''wardrobe''">store your clothes</a>, and a large tall <a href="exec:gt ''mirror'', ''start''">mirror</a>.'
		''
		if servitudeLvl >= 1:'A computer printed <a href="exec:gt ''pavSharedApt'', ''list''">list</a> hangs on the exit door.'
		act 'Living room (your room)':gt 'pavSharedApt', 'pcsRoom'
		act 'Bathroom':gt 'pavSharedApt', 'bathroom'
		act 'Kitchen':gt 'pavSharedApt', 'kitchen'
		act '<font color=#<<$nd_font>>><<$nd_name>>''s room</font>':gt 'pavSharedApt', 'ndRoom'
		act '<font color=#<<$gp_font>>><<$gp_name>>''s room</font>':gt 'pavSharedApt', 'gpRoom'
		act '<font color=#<<$sp_font>>><<$sp_name>>''s room</font>':gt 'pavSharedApt', 'spRoom'
	end

	if home_owned[2] = 0 and kid > 0:
		*clr & cla
		pavIntroStep = 0
		introDone = 0
		minut = 5
		!Need to have some actual story to this with NPC reactions.
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/school/boy/dimka/revenge/crying.jpg"></center>'
		'You''ve been kicked out of the apartment due to having children.'
		pavSharedAptEnabled = 0
		act 'Leave':gt 'pavComplexb3', 'etaj_4'
	end

	act '<font color = maroon>Leave and go to the stairwell.</font>':
		if $clothingworntype ! 'nude':
			gt 'pavComplexb3', 'etaj_4'
		else
			msg'<b><font color = red>You need to get dressed.</font></b>'
			gt 'pavSharedApt', 'start'
		end
	end

	act '<font color = maroon><b>Leave and go outside</b></font>':
		if $clothingworntype ! 'nude':
			gt 'pavResidential'
		else
			msg'<b><font color = red>You need to get dressed.</font></b>'
			gt 'pavSharedApt', 'start'
		end
	end
end

if $ARGS[0] = 'payCash':
	minut += 2
	cash = rentLeft
	if money >= cash:
		'You have decided to pay the remainder of the weekly rent in cash. It makes '+cash+' <b>₽</b>.'
		cashPaid += cash
		money -= cash
		rentPaid = 1
		pavSharedAptEnabled = 1
	else
		'You do not have enough money to pay it.'
	end
	act 'Proceed':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'payService':
	minut += 1
	'The amount '+price+' <b>₽</b> was taken in account of your weekly rent.'
	servicePaid += price
	act 'Continue':gt $loc, $metka
end

if $ARGS[0] = 'rentSetup':
	minut += 2
	'You get a list of paper, saying the weekly rent fee is <<rentWeekly>> rubles.'
	rentPaid = 1
	introDone = 1
	rentWeekNum = (daystart - (daystart mod 7)) / 7 + 1
	act 'Continue':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'intro':
	gs 'pavSharedApt', 'update'
	if step > 0:
		'<center><img border="5" bordercolor="<<$sp_font>>" height="150" src="images/locations/pavlovsk/resident/sharedapt/sp/sp.jpg"> <img height="150" src="images/locations/pavlovsk/resident/sharedapt/gp/gp.jpg"> <img height="150" src="images/locations/pavlovsk/resident/sharedapt/nd/nd.jpg"></center>'
		''
	end

	if pavIntroStep = 0:
		minut += 2
		pavIntroStep = 1
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/sp/sp.jpg"></center>'
		''
		'The door are opened by a young, nice looking athletic man.'
		'<font color=#<<$sp_font>>>"Hi sweetie! What do you need?"</font>'
		'<font color=#<<$pcs_font>>>"Hello, my name is <<$pcs_firstname>> <<$pcs_lastname>>. I''ve seen the advert that someone here have a spare room for rent?"</font>'
		'<font color=#<<$sp_font>>>"Yeah, that''s true. Come in, I''ll call the guys and we can talk about it. I''m <<$sp_name>> by the way."</font>'
		act 'Go in':gt 'pavSharedApt', 'intro'
	elseif pavIntroStep = 1:
		minut += 3
		pavIntroStep = 2
		'You go through the door in a hallway. By the amount of coats and shoes you guess there are about 3 or 4 people living here.'
		'The guy disappears in one of the doors and you can hear talking him to someone.'
		'They seem to be trying to talk quietly, but you can hear a sentence or two..:'
		''
		'"..a girl I tell you.." "..nice one, really, with nice round tits man.." "..better than another dick, idiot.." "..ok ok just be done with it.."'
		''
		'In a while three young guys appear in the hallway.'
		'<font color=#<<$sp_font>>>"So that''s her guys. <<$pcs_firstname>>, could you introduce yourself to guys?"</font>'
		'<font color=#<<$pcs_font>>>"Hello, my name is <<$pcs_firstname>> <<$pcs_lastname>>. I would like to rent the empty room in your apartment."</font>'
		'<font color=#<<$gp_font>>>"Well now that''s what I like! Finally this won''t be such a gay party!"</font>'
		'<font color=#<<$nd_font>>>"<<$pcs_firstname>> please excuse my friend <<$gp_name>>, he is such a rude prick to everyone. My name is <<$nd_name>>."</font>'
		'<font color=#<<$gp_font>>>"Yup, I''m <<$gp_name>>."</font>'
		'<font color=#<<$nd_font>>>"So you would like to live here with us?"</font>'
		'<font color=#<<$pcs_font>>>"Yes. I need a new place to stay."</font>'
		'<font color=#<<$gp_font>>>"How old are you girl?"</font>'
		'<font color=#<<$nd_font>>>"<<$gp_name>>! Not this again!"</font>'
		'<font color=#<<$pcs_font>>>"It''s OK. I am actually <<age>> years old. Why do you ask?"</font>'
		'<font color=#<<$nd_font>>>"He''s just idiot, don''t listen to him."</font>'
		if age < 18:
			'(You can see <<$gp_name>> rub his crotch just a brief moment after you told him your age. What''s he up to?)'
		end
		if pregknown = 1:
			act 'Ask about children':
				cla
				pavIntroStep = 50
				'<font color=#<<$pcs_font>>>"I thought I should tell you, I''m currently pregnant. How you feel about having children in the apartment, will this be a problem?"</font>'
				act 'continue':gt 'pavSharedApt', 'intro'
			end
		elseif kid > 0:
			act 'Ask about children':
				cla
				pavIntroStep = 50
				'<font color=#<<$pcs_font>>>"I thought I should tell, I''m mother. How you feel about having children in the apartment, will this be a problem?"</font>'
				act 'continue':gt 'pavSharedApt', 'intro'
			end
		else
			act 'Continue':gt 'pavSharedApt', 'intro'
		end
	elseif pavIntroStep = 2:
		minut += 3
		pavIntroStep = 3
		'<font color=#<<$pcs_font>>>"So what do you guys say, will you take me in?"</font>'
		'<font color=#<<$nd_font>>>"We may. But first, do you have the money to pay the rent?"</font>'
		'<font color=#<<$pcs_font>>>"Well, of course I don''t expect to let me live here for free. But I hope the rent is reasonable.."</font>'
		'<font color=#<<$nd_font>>>"It is indeed."</font>'
		'<font color=#<<$sp_font>>>"<<rentWeekly>> rubles. Per week."</font>'
		'<font color=#<<$pcs_font>>>"Uhm. That''s quite a lot. I''m not sure if I''ll be able to afford this.."</font>'
		'<font color=#<<$gp_font>>>"You can pay other ways if you''d like!"</font>'
		'<font color=#<<$nd_font>>>"<<$gp_name>>!"</font>'
		'<font color=#<<$pcs_font>>>"What do you mean by other ways?"</font>'
		'<font color=#<<$sp_font>>>"Well <<$pcs_firstname>>, since we are just guys here, and since we suck at these things, we could use someone as you, a girl I mean, to be something like our, let''s say, handmaiden. If you know what I mean."</font>'
		'<font color=#<<$pcs_font>>>"Like cooking and cleaning?"</font>'
		'<font color=#<<$sp_font>>>"Yes, exactly."</font>'
		'<font color=#<<$gp_font>>>"And more."</font>'
		'<font color=#<<$nd_font>>>"Oh my... I give up."</font>'
		act 'More what?':gt 'pavSharedApt', 'intro'
	elseif pavIntroStep = 3:
		minut += 3
		'<font color=#<<$gp_font>>>"More services."</font>'
		'<font color=#<<$pcs_font>>>"What kind of ''more services''?"</font>'
		'<font color=#<<$sp_font>>>"Girl to guys kind of services."</font>'
		'<font color=#<<$pcs_font>>>"You mean to be a whore for you? Instead of paying the rent???"</font>'
		'<font color=#<<$nd_font>>>"Please <<$pcs_firstname>>, don''t get mad. <<$gp_name>> is not just idiot, he is a rude idiot. We are definitely not asking you to be our whore or something like that."</font>'
		'<font color=#<<$nd_font>>>"As <<$sp_name>> told you, we could decrease your rent if you would do some homekeeping stuff we really suck at. But only if you want, of course."</font>'
		'<font color=#<<$sp_font>>>"So what do you say <<$pcs_firstname>>?"</font>'
		act 'Say you will pay the rent the normal way':pavSharedAptEnabled = 1 & pavIntroStep = 10 & gt 'pavSharedApt', 'intro'
		act 'Say you don''t mind doing the homekeeping stuff':pavSharedAptEnabled = 1 & pavIntroStep = 20 & gt 'pavSharedApt', 'intro'
		act 'Say you don''t mind anything':pavSharedAptEnabled = 1 & pavIntroStep = 30 & gt 'pavSharedApt', 'intro'
		act 'Say that you changed your mind about this':pavIntroStep = 40 & gt 'pavSharedApt', 'intro'
	elseif pavIntroStep = 10:
		minut += 2
		'<font color=#<<$sp_font>>>"Fine. So the rest of this week you have for free, but you need to prepay the next week. OK?"</font>'
		'<font color=#<<$pcs_font>>>"Yes, OK, I understand."</font>'
		'<font color=#<<$sp_font>>>"So the room is over there.</font>'
		'<font color=#<<$pcs_font>>>"Wait, Isn''t that the living room?"</font>'
		'<font color=#<<$sp_font>>>"Yes it is, but we don''t use it, so we are renting it out. There is all you need, a bed, a table with chair, all this stuff, we even put a curtain over the doorway. Just move in when you want. See you later <<$pcs_firstname>>!"</font>'
		'<font color=#<<$nd_font>>>"Have a nice day <<$pcs_firstname>>."</font>'
		'<font color=#<<$gp_font>>>"See ya later girlie..."</font>'
		'<font color=#<<$pcs_font>>>"Later guys!"</font>'
		servitudeLvl = 0
		act 'Continue':pavIntroStep = 0 & gt 'pavSharedApt', 'rentSetup'
	elseif pavIntroStep = 20:
		minut += 2
		'<font color=#<<$sp_font>>>"Fine. So the rest of this week you have for free, but you need to prepay the next week. OK?"</font>'
		'<font color=#<<$pcs_font>>>"Yes, OK, I understand."</font>'
		'<font color=#<<$nd_font>>>"We will pepare a list of the charges for the homekeeping services you may want to do, so it''s clear how much will be deducted form your rent."</font>'
		'<font color=#<<$pcs_font>>>"Cool!"</font>'
		'<font color=#<<$sp_font>>>"So the room is over there.</font>'
		'<font color=#<<$pcs_font>>>"Wait, Isn''t that the living room?"</font>'
		'<font color=#<<$sp_font>>>"Yes it is, but we don''t use it, so we are renting it out. There is all you need, a bed, a table with chair, all this stuff, we even put a curtain over the doorway. Just move in when you want. See you later <<$pcs_firstname>>!"</font>'
		'<font color=#<<$nd_font>>>"Have a nice day <<$pcs_firstname>>."</font>'
		'<font color=#<<$gp_font>>>"See ya later girlie..."</font>'
		'<font color=#<<$pcs_font>>>"Later guys!"</font>'
		servitudeLvl = 1
		act 'Continue':pavIntroStep = 0 & gt 'pavSharedApt', 'rentSetup'
	elseif pavIntroStep = 30:
		minut += 2
		'<font color=#<<$sp_font>>>"Fine. So the rest of this week you have for free, but you need to prepay the next week. OK?"</font>'
		'<font color=#<<$pcs_font>>>"Yes, OK, I understand."</font>'
		'<font color=#<<$nd_font>>>"We will pepare a list of the charges for the homekeeping services you may want to do, so it''s clear how much will be deducted form your rent."</font>'
		'<font color=#<<$gp_font>>>"The extra stuff services will be there too, girl."</font>'
		'<font color=#<<$pcs_font>>>"OK.."</font>'
		'<font color=#<<$sp_font>>>"So the room is over there.</font>'
		'<font color=#<<$pcs_font>>>"Wait, Isn''t that the living room?"</font>'
		'<font color=#<<$sp_font>>>"Yes it is, but we don''t use it, so we are renting it out. There is all you need, a bed, a table with chair, all this stuff. Just move in when you want, we even put a curtain over the doorway. See you later <<$pcs_firstname>>!"</font>'
		'<font color=#<<$nd_font>>>"Have a nice day <<$pcs_firstname>>."</font>'
		'<font color=#<<$gp_font>>>"See ya later girlie..."</font>'
		'<font color=#<<$pcs_font>>>"Later guys!"</font>'
		servitudeLvl = 2
		act 'Continue':pavIntroStep = 0 & gt 'pavSharedApt', 'rentSetup'
	elseif pavIntroStep = 40:
		minut += 2
		'<font color=#<<$nd_font>>>"I told you idiot not to scare her away!"</font>'
		'<font color=#<<$gp_font>>>"Sorry man! We can still lock her to the radiator like I suggested in the first place.."</font>'
		'<font color=#<<$nd_font>>>"Just shut up already!"</font>'
		''
		'<font color=#<<$sp_font>>>"Sorry <<$pcs_firstname>>. We respect your decision. If you change your mind and room is still empty, let us know and we can talk again. Goodbye!"</font>'
		act 'Tell them goodbye and leave':pavIntroStep = 0 & gt 'pavComplexb3', 'etaj_4'
	elseif pavIntroStep = 50:
		minut += 2
		!add dialogue for returning after being previously expelled from apartment
		'<font color=#<<$nd_font>>>"I''m sorry <<$pcs_firstname>>, but we don''t want children or pets in the apartment!"</font>'
		'<font color=#<<$gp_font>>>"If you get rid of the kid, come back and see us."</font>'
		'<font color=#<<$sp_font>>>"Don''t mind him. There''s simply not enough room for 4 people, plus kids or pets. I hope you understand"</font>'
		if home_owned[2] = 1:
			act 'Suggest a compromise':
				cla
				pavIntroStep = 2
				'<font color=#<<$pcs_font>>>"I can leave them with my parents for the time being, and keep them away from the apartment, how does this sound?'
				'<font color=#<<$sp_font>>>"Are you certain you want to do this?"</font>'
				'<font color=#<<$gp_font>>>"Sounds good to me!"</font>'
				'<font color=#<<$nd_font>>>"hmmmm"</font>'
				act 'Continue':gt 'pavSharedApt', 'intro'
			end
			act 'I wont leave my children':
				cla
				pavIntroStep = 0
				'<font color=#<<$pcs_font>>>"OK, I will make do with my current accomodation.'
				'<font color=#<<$sp_font>>>"We understand"</font>'
				'<font color=#<<$gp_font>>>"Damn"</font>'
				'<font color=#<<$nd_font>>>"No problem <<$pcs_firstname>>"</font>'
				act 'Continue':gt 'pavSharedApt', 'intro'
			end
		else
			'<font color=#<<$pcs_font>>>OK, I understand.</font>'
			act 'Thank them and leave':pavIntroStep = 0 & gt 'pavComplexb3', 'etaj_4'
		end
	end
end

! the pricelist
if $ARGS[0] = 'list':
	$metka = 'list'
	minut += 2
	$goBack = $metka
	'<center><h3><<$pcs_firstname>>''s pricelist</h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/list.jpg"></center>'
	''
	'The basic weekly rent is <<rentWeekly>> rubles.'
	'It is possible to pay it in cash, or offer services instead. The prices are folllowing:'
	''
	'---------------'
	''
	'Cooking a warm meal: '+price_cook
	'Cleaning the apartment: '+price_clean
	if servitudeLvl >= 2:
		''
		'Handjob: '+price_handjob
		'Blowjob: '+price_blowjob
		'Sex: '+price_vaginal
		'Anal sex: '+price_anal
		'Special treats: '+price_special
	end
	''
	'---------------'
	''
	'Not having paid the full weekly rent at the beginning of a new week will lead to termination of the accommodation services.'
	'Signed: <a href="exec: gt ''pavShared_nd'', ''ndDick''"><font color=#<<$nd_font>>><<$nd_name>></font></a>, <a href="exec: gt ''pavShared_gp'', ''gpDick''"><font color=#<<$gp_font>>><<$gp_name>></font></a>, <a href="exec: gt ''pavShared_sp'', ''spDick''"><font color=#<<$sp_font>>><<$sp_name>></font></a>, <<$pcs_firstname>>.'
	''
	'---------------'
	act 'Continue':gt 'pavSharedApt', 'start'
end

! paying with money
if $ARGS[0] = 'payRent':
	minut += 1
	'You decided to give the guys money, instead of serving them.'
	'You just spent <<paid_rent>> cash.'
	money -= paid_rent
	gs 'stat'
	act 'Continue': gs 'pavSharedApt', 'start'
end

if $ARGS[0] = 'bathroom':
	minut += 1
	$location_type = 'bathroom'
	$loc = 'pavSharedApt'
	$metka = 'bathroom'
	$metkaM = 'bathroom'
	$locM = 'pavSharedApt'

	'<center><h3>Bathroom</h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/vanrpar.jpg"></center>'
	'Modest bathroom, containing all that young people need for their daily life, such as:'
	'A Shower, toilet, <a href="exec:GT ''mirror'',''start''">mirror</a>, where you can ' + iif(pcs_hairbsh = 0,'<a href="exec:GT ''mirror'',''brush''">brush</a>','brush') + ' your hair, a sink and even a bath tub.'

	gs 'din_van', 'private'
	act 'Leave':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'kitchen':
	*clr & cla
	minut += 2
	$loc = 'pavSharedApt'
	$metka = 'kitchen'
	gs 'kit_din'
	'<center><h3>Kitchen</h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/kuhrpar.jpg"></center>'
	'It is a modest kitchen but has all that is needed.'

	! dishes
	if cltarelka > 0:
		'Above the sink are <b><<cltarelka>></b> the clean plates'
	else
		'<b><font color = red>You have no clean dishes left.</font></b>'
	end
	if dirttarelka > 0:'The sink has <b><<dirttarelka>></b> pieces of dirty dishes. <a href="exec: dynamic $dirtarm">That can be washed and</a>.'
	if fairy > 0:
		'above the sink is a detergent to wash the dishes with, there is enough for <b><<fairy>></b> time.'
	else
		'<b><font color = red>you do not have any detergent to wash the dishes, You need to buy detergent.</font></b>'
	end

	! food
	if eda > 0:
		if cltarelka = 0 or edahot > 0:$edagot = ''
		if cltarelka > 0 and edahot = 0:$edagot = '<a href="exec:dynamic $edagotd">Cook meal (0:30) for yourself</a>'
		'In the refrigerator there''s enough (your own) food for <b><<eda>></b> servings. <<$edagot>>'
	end
	if edahot > 0:'<a href="exec: dynamic $edahotd ">On the table is ready meal (0:30).</a>'
	if edaD = 0 and eda = 0:'<b><font color="red">Yyou have nothing to eat, the refrigerator is empty!</font></b>'

	if eda >= 3:
		'<a href="exec: gt ''pavSharedApt'', ''cookForGuys''">Cook meal (1:30) for the guys</a>'
	end

	act 'Have a snack (0:05)':gs 'food', 'snack'

	act 'Drink some water (0:05)':gs 'beverage', 'bev_wat'

	act 'Have a cup of tea (0:05)':
		cls
		minut += 5
		if pcs_hydra >= 100:
			'You don''t need any more tea.'
		else
			pcs_hydra += 80
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			pcs_mood += rand(3, 6)
			'You make a refreshing cup of tea and everything seems a little better.'
		end

		gs 'stat'

		act 'Get up from the table':gt 'pavSharedApt', 'kitchen'
	end

	if pranik > 0:
		'You have <<pranik>> cookies.'

		act 'Drink tea with cookies (0:05)':
			cla
			*clr
			minut += 5
			pranik -= 1
			pcs_health += 30
			pcs_mood += 20
			pcs_energy += 20
			if pcs_hydra >= 100:
				pcs_hydra += 30
			else
				pcs_hydra += 60
			end
			cumspclnt = 2
			gs 'cum_cleanup'
			pcs_breath = 0
			fat += 4
			frost = 0
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
			'You drank tea and enjoyed the cookies. You know they''re too sweet and can''t be good for your figure, but they taste so good!'

			act 'Get up from the table':gt 'pavSharedApt', 'kitchen'
		end
	end

	if edaD > 0:
		act 'Eat diet food (0:30)':
			cla
			*clr
			frost = 0
			minut += 30
			pcs_health += 10
			pcs_mood += 20
			edaD -= 1
			'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
			if pcs_energy >= 100:
				'You prepare a diet meal and try to eat it, but you''re really not hungry at all. You can''t finish the meal.'
			elseif pcs_energy >= 80:
				pcs_energy += 40
				'You try to enjoy the diet meal, but half-way in you realize you weren''t all that hungry yet. You manage to finish the meal, but feel like it was kind of wasted.'
			else
				pcs_energy += 80
				'The diet meal is tasty, and you enjoy eating it.'
			end

			if pcs_hydra >= 80:
				'You don''t need any more tea.'
			else
				pcs_hydra += 100
				cumspclnt = 2
				gs 'cum_cleanup'
				pcs_breath = 0
				'You have a cup of tea, quenching your thirst.'
			end

			act 'Get up from the table':gt 'pavSharedApt', 'kitchen'
		end
	end

	if fatdel > 0:
		act 'Consume fat burners':
			cla
			*clr
			fatdel -= 1
			if pcs_hydra >= 100:
				pcs_hydra += 25
			else
				pcs_hydra += 50
			end
			salo -= 20
			if salo < 11: salo = 11
			gs 'body_shape', 'softreset'

			'You throw the capsule of fat burners in a glass, add some water and drink it. You''re amazed at how fast the capsule works, feeling your body slim down almost immediately.'

			act 'Get up from the table':gt 'pavSharedApt', 'kitchen'
		end
	end

	if lekarstvo > 0:
		'You have <<lekarstvo>> tablet(s).'

		if sick > 0 and lekarday ! daystart:
			act 'Take a pill (0:05)':
				cla
				*clr
				minut += 5
				lekarday = daystart
				lekarstvo -= 1
				sick -= sick * 20 / 100
				if pcs_hydra >= 100:
					pcs_hydra += 25
				else
					pcs_hydra += 50
				end
				picrand = rand(1,9)
				'<center><img <<$set_imgh>> src="images/locations/shared/apartment/event/sick/lekr<<picrand>>.jpg"></center>'
				'You swallow the pill, and shortly after you feel a bit better.'

				act 'Continue':gt 'pavSharedApt', 'kitchen'
			end
		end
	end

	if vitamin > 0:
		'You have <<vitamin>> vitamin pills.'

		if vitaminday ! daystart:
			act 'Take vitamins (0:05)':
				cla
				*clr

				minut += 5
				vitaminday = daystart
				vitamin -= 1
				frost = 0
				if pcs_hydra >= 100:
					pcs_hydra += 25
				else
					pcs_hydra += 50
				end
				if KandidozOnce = 1:Kandidoz -= 2
				if GerpesOnce = 1:Gerpes -= 2
				if SifacOnce = 1 and Sifilis >= 10:Sifilis -= rand(0, 1)

				pcs_skin += 1

				'You take some vitamins to stay healthy.'

				act 'Continue':gt 'pavSharedApt', 'kitchen'
			end
		end
	end

	act 'Leave':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'cookForGuys':
	cla
	*clr
	$loc = 'pavSharedApt'
	$metka = 'kitchen'
	minut += 90
	eda -= 3
	kuhdrova = 0
	pcs_mood -= 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/shared/home/kitchen/cook.jpg"></center>'
	'You prepare three meals.'
	price = price_cook

	act 'Leave the meal':gt 'pavSharedApt', 'payService'
end

if $ARGS[0] = 'cleanForGuys':
	cla
	*clr
	minut += 60
	poroshok -= 3
	pcs_mood -= 10
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/apartment/stir.jpg"></center>'

	price = price_clean
	*nl
	'You collect piles piles of dirty clothes around the room, carry them into the bathroom, put them in a basin and then pour washing powder on them. You wash them until they are finally clean.'
	cumStainCh =  rand (0, 100)
	if servitudeLvl = 2 and cumStainCh <= 50:'<i>You notice bedsheets with what seems as dried out cum. Probably remainder of one of yours "servings". Maybe it is really better to swallow everytime, you wonder?</i>'

	act 'Hang them to dry out and leave':gt 'pavSharedApt', 'payService'
end

! living room & pc bedroom
if $ARGS[0] = 'pcsRoom':
	$loc = 'pavSharedApt'
	$metka = 'pcsRoom'
	$location_type = 'private'
	$locclass = 'bedr'
	minut += 2
	gs 'stat'
	gs 'pavSharedApt', 'update'

	'<center><b><font color="maroon">Living Room</font></b></center>'
	'<center><img <<$set_imgh>> src="images/shared/sharedapt/bedroom/sofabed.jpg"></center>'
	''
	'The living room wasn''t in use by the other apartment occupants, so it is now rented to use as a bedroom. It''s sparsly decorated, with, a <a href="exec:gt ''bed'', ''start''">sofa bed</a>, against the back wall.'
	''
	'Beside the sofa bed, is an old cabinet, with your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> on top.'
	if budilnikOn = 0:
		'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set for <<timer>> hours <<timerM>> minutes and currently turned <a href="exec: budilnikOn = 1 & GT ''pavSharedApt'',''pcsRoom''">off</a>.'
	else
		'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set for <<timer>> hours <<timerM>> minutes and currently turned <a href="exec: budilnikOn = 0 & GT ''pavSharedApt'',''pcsRoom''">on</a>.'
	end
	''
	if TV = 0:$TV = 'a great place for a TV, if you had one.'
	if TV = 1:$TV = 'an old CRT <a href="exec:gt ''TV'', ''start''">TV</a> sits on the shelf'
	'There is an wall unit at the other side of the room, <<$TV>>. Everyone in the household spends their time in their own rooms, so there was never a reason to use the living room for its intended purpose.'
	''
	'On the other side of the sofa bed are your old <a href="exec:gt ''clothing'', ''view_clothing_list'', ''store''">suitcases</a>, which you could use to store the clothes you don''t often wear.'
	''
	'There is no door to the living room, instead there''s <a href="exec:view''images/shared/sharedapt/bedroom/curtain.jpg''">curtain</a> covering entry to the room, which gives you a modicum of privacy; not much better than the state of the non-existant window curtains'

	act 'Sit on the sofa bed':gt 'pavSharedApt', 'sofabed'

	if obruch > 0: '<br>A hula hoop stands in corner of the room. '
	if skak > 0: 'A jump rope is on a shelf. '
	if bookYog > 0: 'A yoga mat is rolled up in a corner. '
	gs 'exercise', 'start'
	''

	if komp = 1:'Your <a href="exec:gorodokKomp = 1 & GT ''Komp'',''start''">laptop</a> is sitting on small table.'

	if student > 0 and kursovik < 100:
		act 'Do coursework (1:00)':
			cla
			*clr
			minut += 60
			kursovik += rand(pcs_intel / 20, pcs_intel / 10)

			if kursovik > 100:kursovik = 100

			pcs_mood -= 10
			'<center><img <<$set_imgh>> src="images/pc/activities/study.jpg"></center>'
			'You sit down on the sofa bed and do your coursework, you''ve finished <<kursovik>> percent of it.'

			act 'Stand up':gt 'pavSharedApt', 'pcsRoom'
		end
	end

	if vcard > 0:
		act 'Look at your deck of vintage porn cards':card_deck = 0 & gt 'card'
	end
	if card > 0:
		act 'Look at your deck of new porn cards':card_deck = 1 & gt 'card'
	end

	if dur > 0:'You have <<dur>> doses of Pale Lady, hidden from the other housemates.'

	if dur > 0 and narkday ! daystart:
		act 'Use some Pale Lady (0:05)':
			cla
			*clr
			minut += 5
			narkday = daystart
			pcs_health = pcs_vital * 10 + pcs_stren * 5
			pcs_willpwr = pcs_intel * 5 + pcs_sprt * 5
			pcs_mood = 100
			pcs_horny = 100
			nark += 10
			dur -= 1
			'<center><img <<$set_imgh>> src="images/shared/drugs/dur.jpg"></center>'
			'You snort some Pale Lady, feeling dizzy for just a moment. After that you feel fantastic, and incredibly horny.'

			act 'Continue':gt 'pavSharedApt', 'pcsRoom'
		end
	end

	if gobelen>0:'You have <<gobelen>> completed tapestries.'
	if pcs_sewng >= 40 and tkan > 0:'You have enough fabric for <<tkan>> lessons.'
	if pcs_sewng >= 40 and tkan <= 0:'You do not have any fabric.'
	if pcs_sewng >= 80:
		if newgobelen = 0 and tkan > 0:
			act'Start a new tapestry':
				cls
				tkan -= 1
				newgobelen = 1
				minut += 15
				gs'stat'
				'You spend 15 minutes at the embroidery frame, preparing the fabric and outlining the pattern for the tapestry.'
				act'Set the tapestry aside':gt 'pavSharedApt', 'pcsRoom'
			end
		end
		if newgobelen>=1:
			'Your tapestry is <<newgobelen/10>> percent finished.'
			act'Work on the tapestry for an hour':
				cls
				minut+=60
				sewng_exp += rand(pcs_intel/10, pcs_intel/5)
				gobramax = (pcs_sewng - 70) * 6
				gobramin = (pcs_sewng - 70) * 2
				newgobelen += rand(gobramin,gobramax)
				gs'stat'
				if newgobelen < 1000:'You spent an hour working on your tapestry, which is now <<newgobelen/10>> percent finished.'
					if newgobelen >= 1000:
					newgobelen = 0
					gobelen += 1
					'Your work on the tapestry is done.'
				end
				act'Set the tapestry aside':gt 'pavSharedApt', 'pcsRoom'
			end
		end
	end

	if tabletki > 0 and prezik + prezikcount + preziksabo > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> tablets are in the cabinet next to your bed, along with some <a href="exec:gt ''stol'', ''bc''">condoms</a>.'
	elseif tabletki > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> tablets are in the cabinet next to your bed.'
	elseif prezik + prezikcount + preziksabo > 0:
		'Your <a href="exec:gt ''stol'', ''bc''">condoms</a> are in the cabinet next to your bed.'
	end
	''
	if sewing_kit = 1:
		'Your <a href="exec:gt ''sewing'',''start''">sewing kit</a> is stored on a shelf.'
	end

	if tovarL > 0:'You currently have <<tovarL>> trinkets to sell at the station. The maximum you can carry with you is 30.'

	dynamic $wearpan
	dynamic $removepan
	!gs 'subkid'
	if pcs_sleep <= 80:
		if $clothingworntype = 'nude':
			$sleep_txt = 'Go to sleep'
		else
			$sleep_txt = 'Undress and go to sleep'
		end
		if succubusQW = 2:
			act '<<$sleep_txt>>':
				if $clothingworntype ! 'nude': gs 'clothing', 'strip'
				gt 'bed2', 'succhoice'
			end
		else
			act '<<$sleep_txt>>':
				if disable_autosave = 0:
					if StoryLine = 0:
						savegame 'autosave.sav'
					else
						savegame 'autosave_alt.sav'
					end
				end
				if $clothingworntype ! 'nude': gs 'clothing', 'strip'
				if magf2bdo = 2:
					gt 'bed2', 'magbstchoice'
				else
					gt 'bed2', 'sleep'
				end
			end
			killvar '$sleep_txt'
		end
	end
	act 'Leave the room':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'sofabed':
	$loc = 'pavSharedApt'
	$metka = 'sofabed'
	*clr & cla
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/shared/sharedapt/bedroom/sofabed.jpg"></center>'
	'<center>A small sofa bed, when not unfolded, is just big enough for you to curl up on and take a nap.</center>'

	if TV = 1:
		act 'Watch some TV':gt 'TV', 'start'
	end

	if pornMagazine > 0:'<br>There is a porn magazine under the sofa cushions'

	gs 'events', 'read_porn'
	gs 'events', 'read'

	if pcs_horny >= 50:
		act 'Masturbate':gt 'selfplay', 'start'
	end

	act 'Take a nap (1:00)':
		if pcs_sleep < 90:
			pcs_sleep += 10
			pcs_health += 5
			minut += 60
			gs 'stat'
			'You sleep about an hour.'
		else
			minut += 5
			gs 'stat'
			'You are not tired enough to sleep, even for a short nap.'
		end
	end
	act 'Get up':gt 'pavSharedApt', 'pcsRoom'
end

if $ARGS[0] = 'ndRoom':
	$loc = 'pavSharedApt'
	$metka = 'ndRoom'
	minut += 2
	'<center><h3><font color=#<<$nd_font>>><<$nd_name>>''s room</font></h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/nd/room.jpg"></center>'
	''
	'The room is surprisingly not as messy as one would expect, given that <<$nd_name>> is an IT guy.'
	'There is one large window with bed bellow it, but the whole room is dominated by a large table with several computers and monitors.'
	'Looking at it one would say that this must be a den of a hacker.'
	if hour >= nd_sleeps and hour < nd_wakes:
		'<<$nd_name>> is sleeping in his bed.'
	elseif hour >= nd_wakes and hour < nd_leaves:
		'<<$nd_name>> is already awake, getting ready to leave.'
		act 'Approach him': gt 'pavShared_nd', 'nd'
	elseif hour >= nd_leaves and hour < nd_comes:
		'<<$nd_name>> is not here.'
	elseif hour >= nd_comes and hour < nd_sleeps:
		'<<$nd_name>> is currently behind his desk, doing some clever IT stuff.'
		act 'Approach him': gt 'pavShared_nd', 'nd'
	end
	if servitudeLvl >= 1 and nd_cleaned = 0:act 'Clean the room':nd_cleaned = 1 & gt 'pavSharedApt', 'cleanForGuys'
	act 'Leave':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'gpRoom':
	$loc = 'pavSharedApt'
	$metka = 'gpRoom'
	minut += 2
	'<center><h3><font color=#<<$gp_font>>><<$gp_name>>''s room</font></h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/gp/room.jpg"></center>'
	''
	'Messy room. Clothes everywhere.'

	if hour >= gp_wakes and hour < gp_leaves:
		'<<$gp_name>> is already awake, getting ready to leave.'
		act 'Approach him': gt 'pavShared_gp', 'gp'
	elseif hour >= gp_leaves and hour < gp_comes:
		'<<$gp_name>> is not here.'
	elseif hour >= gp_comes and hour < gp_sleeps:
		'<<$gp_name>> is currently on his bed, listing through some magazines.'
		act 'Approach him': gt 'pavShared_gp', 'gp'
	elseif hour >= gp_sleeps or hour < gp_wakes:
		'<<$gp_name>> is sleeping in his bed.'
	end
	if servitudeLvl >= 1 and gp_cleaned = 0:act 'Clean the room':gp_cleaned = 1 & gt 'pavSharedApt', 'cleanForGuys'
	act 'Leave':gt 'pavSharedApt', 'start'
end

if $ARGS[0] = 'spRoom':
	$loc = 'pavSharedApt'
	$metka = 'spRoom'
	minut += 2
	'<center><h3><font color=#<<$sp_font>>><<$sp_name>>''s room</font></h3></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/sharedapt/sp/room.jpg"></center>'
	''
	'Tidy room full of sporting stuff like dresses, equipment and trophys. On the walls hang several posters of famous sport guys.'

	if hour >= sp_wakes and hour < sp_leaves:
		'<<$sp_name>> is already awake, packing his bags to leave.'
		act 'Approach him': gt 'pavShared_sp', 'sp'
	elseif hour >= sp_leaves and hour < sp_comes:
		'<<$sp_name>> is not here.'
	elseif hour >= sp_comes and hour < sp_sleeps:
		doesStuff = rand (0, 100)
		if doesStuff < 50:'<<$sp_name>> is currently on his bed, listing through sport magazines.'
		if doesStuff >= 50:'<<$sp_name>> is working out on the floor.'
		act 'Approach him': gt 'pavShared_sp', 'sp'
	elseif hour >= sp_sleeps or hour < sp_wakes:
		'<<$sp_name>> is sleeping in his bed.'
	end
	if servitudeLvl >= 1 and sp_cleaned = 0:act 'Clean the room':sp_cleaned = 1 & gt 'pavSharedApt', 'cleanForGuys'
	act 'Leave':gt 'pavSharedApt', 'start'
end

--- pavSharedApt ---------------------------------


# kiosk

!!WD:"SUB >= 15" for act - Will need balancing / playtesting

if $kioskloc = 'counter' and $args[0] ! 'counter': gt 'kiosk', 'counter'

if $ARGS[0] = 'start':
	if hour >= 14 and hour <= 16:
		cls
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk21.jpg"></center>'
		'It is currently very busy and looks like you''ll have to wait to get to the counter.'
		
		act 'Leave': minut += 1 & gt $loc
		
		act 'Wait in line (20 Minutes)':
			minut += 20
			gt 'kiosk', 'counter'
		end

		if pcs_exhibition >= 45:
			act 'Flash both your tits and your pussy':
				cls
				minut += 1
				exhibition_exp += rand(12,15)
				gs 'events', 'flash_full', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if pcs_exhibition >= 40:
			act 'Flash your pussy':
				cls
				minut += 1
				exhibition_exp += rand(9,12)
				gs 'events', 'flash_pussy', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if pcs_exhibition >= 35:
			act 'Flash your tits':
				cls
				minut += 1
				exhibition_exp += rand(5,8)
				gs 'events', 'flash_tits', 'outdoors', 5
				gs 'stat'
				
				act 'Go to the counter': gt 'kiosk', 'counter'
			end
		end

		if sub >= 15:
			act 'Suggest a blowjob in exchange for jumping the queue':
				if cumloc[12] = 0 and cumloc[11] = 0:
					cls
					pcs_throat += 1
					gs 'npcgeneratec', 0, 'stranger from the post office', rand(19,45)
					gs 'boyStat', $npclastgenerated

					exhibition_exp += rand(5,8)
					'<center><img <<$set_imgh>> src="images/shared/sex/blowjob/kiosk_blowjob.jpg"></center>'
					'A man agrees to your suggestion and lets you move in front of him.'

					gs 'arousal', 'bj', 10, 'sub'
					gs 'stat'
					
					act 'Cum on your face':
						cls
						'<center><img <<$set_imgh>> src="images/pc/body/cum/cumface/cumface' + rand(1,23) + '.jpg"></center>'
						'The man cums on your face.'
						spafinloc = 11
						gs 'cum_manage'

						gs 'arousal', 'end'
						gs 'stat'
						
						act 'Go to the counter': gt 'kiosk', 'counter'
					end

					act 'Cum inside your mouth':
						cls
						'<center><img <<$set_imgh>> src="images/locations/shared/postoffice/sex/cumlip.jpg"></center>'
						'The man cums inside your mouth.'
						spafinloc = 12
						gs 'cum_manage'

						gs 'arousal', 'end'
						gs 'stat'
						
						act 'Go to the counter': gt 'kiosk', 'counter'
					end
				else
					cls
					'<center><video autoplay loop src="images/locations/shared/postoffice/punch.mp4"></video></center>'	
					'The Man punches you in the gut and says, <font color="#0D42C0">"Go away bitch, your mouth is full of sperm."</font> You stagger away from the kiosk and spend 2 hours curled up in a ball.'
					minut += 120
					gs 'pain', 4, 'tummy', 'hit'
					gs 'stat'
					
					act 'Recover':gt $loc
				end
			end
		end
	else
		gt 'kiosk', 'counter'
	end
end

if $ARGS[0] = 'counter':
	cls
	$kioskloc = 'counter'
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk123.jpg"></center>'
	'<center><img WIDTH = 245 HEIGHT = 56 src="images/locations/city/shared/kiosk/ruch.jpg"></center>'
	if $message ! '':
		'<center>'+$message+'</center>'
		killvar '$message'
	else
		''
	end	
	*pl '<center><table align="center" width=80%>' 
	*pl '<center><table align="center">'
	*P FUNC ('$stock_item', 20, 'Cigarettes', 115, 'siga', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Fashion magazine', 430, 'fashmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Computer magazine', 430, 'compmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Biographical magazine', 430, 'biomag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Cooking magazine', 430, 'cookmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Knitting magazine', 430, 'knitmag', 'kiosk', 'counter')
	*P FUNC ('$single_stock_item', 1, 'Fitness magazine', 430, 'fitmag', 'kiosk', 'counter')
	'</table></center>'
	*pl

	act 'Leave': $kioskloc = '' & minut += 1 & gt $loc

	act 'Buy a snack (100 <b>₽</b>) (0:05)':
		*clr & cla
		gs 'stat'
		minut += 5
		money -= 100
		pcs_health += 10
		pcs_mood += 20
		fat += 6
		pcs_energy += 20
		if pcs_hydra >= 100:
			pcs_hydra += 10
		else
			pcs_hydra += 20
		end
		cumspclnt = 2
		gs 'cum_cleanup'
		pcs_breath = 0
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/shared/food/food.jpg"></center>'
		'You enjoy a small tasty, but somewhat fattening, snack.'
		act 'return': gt 'kiosk', 'start'
	end

	act 'Buy some water (40 <b>₽</b>) (0:05)':
		*clr & cla
		frost = 1
		alko -= 1
		minut += 5
		money -= 40
		pcs_health += 10
		pcs_energy += 4
		if pcs_hydra >= 100:
			pcs_hydra += 25
		else
			pcs_hydra += 50
		end
		cumspclnt = 2
		gs 'cum_cleanup'
		pcs_breath = 0
		gs 'cum_cleanup'
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/shared/brothel/barorderwater.jpg"></center>'
		'You enjoy a drink of water'
		act 'return': gt 'kiosk', 'start'
	end

	act 'View phone themes': $kioskloc = '' & gt 'kiosk', 'themes'
end

if $ARGS[0] =  'themes':
	cls
	gs 'stat'
	
	'<center><img <<$set_imgh>> src="images/locations/city/shared/kiosk/kiosk123.jpg"></center>'
	'<center><img WIDTH = 245 HEIGHT = 56 src="images/locations/city/shared/kiosk/ruch.jpg"></center>'
	'All themes cost 500 <b>₽</b>'
	
	if money >= 500:
		i = 1
		:phonethemeloop
		if phonetheme[i] = 0: '<a href="exec: money -= 500 & phonetheme[<<i>>] = 1 & gt ''kiosk'', ''themes''">Buy the <<$phonetheme_name[i]>> theme.</a>'
		i += 1
		if i < 13: jump 'phonethemeloop'
	else
		'You don''t have enough money to buy a phone theme.'
	end

	act 'Return': gt 'kiosk', 'counter'
end

--- kiosk ---------------------------------


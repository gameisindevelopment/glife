# sportShop

if $ARGS[0] = 'start':
    $location_type = 'public_indoors'
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	if night_mode = 1:
		fcolor = rgb(255, 255, 255)
		bcolor = rgb(0, 0, 0)
		lcolor = rgb(106, 90, 205)
	else
		fcolor = rgb(0, 0, 0)
		bcolor = rgb(255, 255, 255)
		lcolor = rgb(106, 90, 205)
	end
	'<center><b><font color="maroon">Danilovich sporting goods</font></b></center>'
	'<center><img src="images/locations/city/citycenter/mall/sportshop/sportshop.jpg"></center>'
	*nl
	'As soon as you enter, a high energy song with a thumping baseline fills your eardrums, making you feeling pumped and awake. The store is sleek and professional, the walls and floors advertising all different kinds of sporting equipment and attire. The room is filled with beautifully fit people shopping, their bodies varying from big and muscular to lean and trim. If they don''t inspire you to want to work out, the posters advertising past and present famous athletes is motivating as hell.'

	act 'Leave':
		cla
		minut += 3
		gt 'torgcentr'
	end

	act 'View sports clothes':
		cla
		minut += 1
		gt 'sportshop', 'clo'
	end

	act 'View exercise shoes':
		minut += 1
		gt 'sportshop', 'trainers'
	end

	act 'View swimwear':
		cla
		minut += 1
		gt 'sportshop', 'swim'
	end

	act 'View sports bras':
		cla
		minut += 1
		gt 'sportshop', 'bras'
	end

	act 'View sports panties':
		cla
		minut += 1
		gt 'sportshop', 'panties'
	end

	if skak = 0 and money >= 500:
		act 'Buy skipping rope (500 <b>₽</b>)':
			cla
			minut += 5
			money -= 500
			skak = 1

			act 'Pick up a package':gt 'sportshop', 'start'
		end
	end

	if obruch = 0 and money >= 1500:
		act 'Buy hoop (1,500 <b>₽</b>)':
			cla
			minut += 5
			money -= 1500
			obruch = 1

			act 'Pick up a package':gt 'sportshop', 'start'
		end
	end

	if bookYog = 0 and money >= 1500:
		act 'Buy benefits of yoga (1,500 <b>₽</b>)':
			cla
			minut += 5
			money -= 1500
			bookYog = 1

			act 'Pick up a package':gt 'sportshop', 'start'
		end
	end
end

if $ARGS[0] = 'clo':
	$metka = 'clo'
	$loc = 'sportShop'
	cls
	gs'stat'
	'<center><b><font color="maroon">Sportswear - one size fits all</font></b></center>'
	*nl

	gs 'clothing_QV', 'shop_header'

	act 'Return':
		cla
		minut += 5
		gt 'sportshop', 'start'
	end

	if Clothingstock > 16:
		i = 1
	elseif Clothingstock > 8:
		i = 2
	else
		i = 3
	end

	:loopexercise
		if exercise[i] = 0:
			gs 'clothing_attributes', 'exercise', i
			gs 'clothing_QV', 'shop_filter'
				
			if clothingfilter_risk = 1 and clothingfilter_pandb = 1 and clothingfilter_norm2 = 1: *p '<a href="exec: i = <<i>> & gt ''sportshop'', ''exercise''"><img src="images/pc/clothing/7exercise/<<i>>.jpg" 	height="250" /></a>'
		end
		i += 3
	if i <= 113:jump 'loopexercise'
end

if $ARGS[0] = 'exercise': gt 'clothing', 'view_clothing_item', 'shop', 'exercise',i, 2000

if $ARGS[0] = 'trainers':
	$metka = 'trainers'
	$loc = 'sportShop'
	cls
	gs'stat'

	act 'Return':
		cla
		minut += 5
		gt 'sportShop', 'start'
	end

	i = 1
	:looptrainers_shoes
		if strainers[i] = 0:*p '<a href="exec: i = <<i>> & gt ''sportShop'', ''training_shoes''"><img src="images/pc/shoes/trainers/<<i>>.jpg" height="250" /></a>'
		i += 1
	if i <= 29:jump 'looptrainers_shoes'
end

if $ARGS[0] = 'training_shoes': gt 'shoes', 'view_shoe_item', 'shop', 'trainers',i, 1500

if $ARGS[0] = 'swim':
	$metka = 'swim'
	$loc = 'sportShop'
	cls
	gs'stat'
	'<center><b><font color="maroon">Swimwear - one size fits all</font></b></center>'
	*nl

	gs 'clothing_QV', 'shop_header'

	act 'Return':
		cla
		minut += 5
		gt 'sportshop', 'start'
	end

!!4 is in he supermarket, so skipped
	if Clothingstock > 18:
		i = 1
	elseif Clothingstock > 12:
		i = 2
	elseif Clothingstock > 6:
		i = 3
	else
		i = 8
	end

	:loopswim
		if swimwear[i] = 0:
			gs 'clothing_attributes', 'swimwear', i
			gs 'clothing_QV', 'shop_filter'
				
			if clothingfilter_risk = 1 and clothingfilter_pandb = 1 and clothingfilter_norm2 = 1: *p '<a href="exec: i = <<i>> & gt ''sportshop'', ''swim1''"><img src="images/pc/clothing/12swimwear/<<i>>.jpg" 	height="250" /></a>'
		end
		i += 4
	if i <= 135:jump 'loopswim'
end

if $ARGS[0] = 'swim1': gt 'clothing', 'view_clothing_item', 'shop', 'swimwear',i, 1750

if $ARGS[0] = 'panties':
	$metka = 'panties'
	$loc = 'sportShop'
	cls
	gs'stat'

	act 'Return':
		cla
		minut += 5
		gt 'sportShop', 'start'
	end

	i = 1

	:loopsport_panties
		if sport_panties[i] = 0:*p '<a href="exec: i = <<i>> & gt ''sportShop'', ''panties2''"><img src="images/pc/panties/sport/<<i>>.jpg" 	height="150" /></a> '
		i += 1
	if i <= 8:jump 'loopsport_panties'
end

if $ARGS[0] = 'panties2':
	cla
	
	if sport_panties[i] ! 0:
		msg 'You already own these panties.'
		gt 'sportShop', 'panties'
	end

	gt 'panties', 'view_panty_item', 'shop', 'sport',i, 1000

end

if $ARGS[0] = 'bras':
	$metka = 'bras'
	$loc = 'sportShop'
	cls
	gs'stat'

	act 'Return':
		cla
		minut += 5
		gt 'sportShop', 'start'
	end

	i = 1

	:loopsport_bras
		if sport_bras[i] = 0:*p '<a href="exec: i = <<i>> & gt ''sportShop'', ''bras2''"><img src="images/pc/bras/sport/<<i>>.jpg" 	height="150" /></a> '
		i += 1
	if i <= 8:jump 'loopsport_bras'
end

if $ARGS[0] = 'bras2':
	cla
	
	if sport_bras[i] ! 0:
		msg 'You already own this bra.'
		gt 'sportShop', 'bras'
	end

	gt 'bras', 'view_bra_item', 'shop', 'sport',i, 1000

end

--- sportShop ---------------------------------

